plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
    id 'java-library'
}

repositories {
    jcenter()
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Re11oy"
            name "Kirill Talaluev"
            email "k@talaluev.me"
        }
    }

    scm {
        url "https://github.com/celo-org/celo-monorepo"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            groupId 'org.celo'
            artifactId 'contractkit'
            version '0.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'CELO Java SDK')
                root.appendNode('name', 'CELO Java SDK')
                root.appendNode('url', 'https://github.com/celo-org/celo-monorepo')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'celo'
        name = 'org.celo:contractkit'
        userOrg = 'celo'
        websiteUrl = 'https://github.com/celo-org/celo-monorepo'
        vcsUrl = 'https://github.com/celo-org/celo-monorepo'
        licenses = ["Apache-2.0"]
        version {
            name = '0.0.1'
            desc = '0.0.1'
            released  = new Date()
            vcsTag = '0.0.1'
        }
    }

}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1',
            'org.web3j:core:4.6.3'

    implementation 'com.google.guava:guava:29.0-jre',
            'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'junit:junit:4.13'
    testCompile 'org.mockito:mockito-core:2.+'
}
